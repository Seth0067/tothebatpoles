ext {
	configFile = file('build.properties')
	config = parseConfig(configFile)
}

def expandVersion (String version) {
	return "${config.mc}-${version}"
}	

def parseConfig(File config) {
    config.withReader {
        def prop = new Properties()
        prop.load(it)
        return (new ConfigSlurper().parse(prop))
    }
}

defaultTasks 'build', 'clean'

buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'

version = expandVersion(config.mod)
group = "com.seth0067.${config.id}"
archivesBaseName = config.id

sourceCompatibility = targetCompatibility = config.java
compileJava {
    sourceCompatibility = targetCompatibility = config.java
}

minecraft {
    version = expandVersion(config.forge)
    runDir = "run"
    mappings = config.mappings
   	replaceIn "Main.java"
	replace "%VERSION%", project.version
	replace "%ID%", config.id
}

dependencies {

}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

jar {
	destinationDir = file("RELEASE")
}
